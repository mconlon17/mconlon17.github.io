# Count papers in 2015 by units in the College of Liberal Arts and Sciences
# If the author list contains authors from multiple units in the college,
# the paper is counted under each unit
#
SELECT ?hlabel (COUNT(DISTINCT ?x) AS ?count)
WHERE {
    ?x a bibo:AcademicArticle .
    ?x vivo:dateTimeValue ?dv .
    ?dv vivo:dateTime ?d .
    FILTER(regex(?d,"^2014"))
	
    ?x vivo:relatedBy ?a .
    ?a a vivo:Authorship .
    ?a vivo:relates ?auth .
    ?auth a ufVivo:UFEntity .
    ?auth a foaf:Person .

    {<http://vivo.ufl.edu/individual/n8858828> ufVivo:homeDeptFor ?auth .
     <http://vivo.ufl.edu/individual/n8858828> rdfs:label ?hlabel .}
    UNION
    {<http://vivo.ufl.edu/individual/n8858828> obo:BFO_0000051 ?o .
    ?o ufVivo:homeDeptFor ?auth . ?o rdfs:label ?hlabel .}
    }
GROUP BY ?hlabel
ORDER BY DESC(?count)