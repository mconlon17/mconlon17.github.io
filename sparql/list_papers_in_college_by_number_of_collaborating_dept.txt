# List papers in the College of Medicine for 2014.
# For each paper count the number of UF participating departments in
# the author list.
#
SELECT ?uri (count(distinct ?hname) as ?ndept) (min(distinct ?title) as ?title)
WHERE {
    ?uri a bibo:AcademicArticle .
    ?uri vivo:dateTimeValue ?dv .
    ?dv vivo:dateTime ?d .
    FILTER(regex(?d,"^2014"))
    ?uri rdfs:label ?title .

    ?uri vivo:relatedBy ?a .
    ?a a vivo:Authorship .
    ?a vivo:relates ?p .
    ?p a foaf:Person .
    ?p a ufVivo:UFEntity .

    {<http://vivo.ufl.edu/individual/n166017> ufVivo:homeDeptFor ?p .
     <http://vivo.ufl.edu/individual/n166017> rdfs:label ?hname .}
    UNION
    {<http://vivo.ufl.edu/individual/n166017> obo:BFO_0000051 ?o .
    ?o ufVivo:homeDeptFor ?p . ?o rdfs:label ?hname .}
}
GROUP BY ?uri
ORDER BY DESC(?ndept)